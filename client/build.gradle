
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

Properties signingProps = new Properties()
File signingPropsFile = new File("client/signing.properties")
if (signingPropsFile.isFile()) {
  signingProps.load(new FileInputStream(signingPropsFile))
}

static def getVersionCodeFromGit() {
  // Same as running:
  // git rev-list <checked out branch name> | wc -l
  // TODO:
  // return Grgit.open(dir: project.buildscript.sourceFile.parentFile.parent).log().size()
  return 1020
}

android {
  compileSdkVersion 33
  buildToolsVersion '33.0.0'

  defaultConfig {
    applicationId "au.com.codeka.warworlds2"
    minSdkVersion 21
    targetSdkVersion 33
    versionCode getVersionCodeFromGit()
    versionName "2.0"
    multiDexEnabled true
    vectorDrawables.useSupportLibrary = true
  }
  buildFeatures {
    viewBinding true
  }
  sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
  }
  compileOptions {
    coreLibraryDesugaringEnabled true

    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
  signingConfigs {
    release {
      storeFile file(signingProps['keystore'] == null ? "/tmp/non-existant" : signingProps['keystore'])
      storePassword signingProps['password']
      keyAlias signingProps['alias']
      keyPassword signingProps['password']
    }
  }

  configurations {
    // This seems like a weird bug... something else includes listenablefuture from guava.
    all*.exclude group: 'com.google.guava', module: 'listenablefuture'
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
      signingConfig signingConfigs.release

      buildConfigField "String", "DEFAULT_SERVER", "\"http://game2.war-worlds.com/\""
    }
    debug {
      buildConfigField "String", "DEFAULT_SERVER", "\"http://127.0.0.1:8080/\""
    }
  }
    packagingOptions {
        resources {
            excludes += ['LICENSE-EPL-1.0.txt', 'LICENSE-EDL-1.0.txt']
        }
    }
    namespace 'au.com.codeka.warworlds.client'
}

dependencies {
  coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.2.0'

  implementation 'androidx.appcompat:appcompat:1.5.1'
  implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
  implementation 'androidx.core:core-ktx:1.9.0'
  implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1'
  implementation 'androidx.preference:preference-ktx:1.2.0'
  implementation 'androidx.transition:transition:1.4.1'
  implementation 'com.google.android.material:material:1.8.0-alpha02'
  implementation 'com.google.android.gms:play-services-base:18.1.0'
  implementation 'com.google.android.gms:play-services-auth:20.3.0'
  implementation 'com.google.firebase:firebase-core:21.1.1'
  implementation 'com.google.firebase:firebase-messaging:23.1.0'
  implementation 'com.google.guava:guava:24.1-android'
  implementation 'com.squareup.picasso:picasso:2.71828'
  implementation 'com.squareup.wire:wire-runtime:4.4.1'
  implementation 'com.android.support:multidex:1.0.3'
  implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
  implementation project(':common')
}

apply plugin: 'com.google.gms.google-services'