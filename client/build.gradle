buildscript {
  repositories {
    jcenter()
  }

  dependencies {
    classpath 'org.ajoberstar.grgit:grgit-core:3.1.1'
  }
}

import org.ajoberstar.grgit.Grgit

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

Properties signingProps = new Properties()
File signingPropsFile = new File("client/signing.properties")
if (signingPropsFile.isFile()) {
  signingProps.load(new FileInputStream(signingPropsFile))
}

android {
  compileSdkVersion 30
  buildToolsVersion '30.0.2'

  defaultConfig {
    applicationId "au.com.codeka.warworlds2"
    minSdkVersion 21
    targetSdkVersion 30
    versionCode getVersionCodeFromGit()
    versionName "2.0"
    multiDexEnabled true
    vectorDrawables.useSupportLibrary = true
  }
  sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
  }
  compileOptions {
    coreLibraryDesugaringEnabled true

    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
  signingConfigs {
    release {
      storeFile file(signingProps['keystore'] == null ? "/tmp/non-existant" : signingProps['keystore'])
      storePassword signingProps['password']
      keyAlias signingProps['alias']
      keyPassword signingProps['password']
    }
  }

  configurations {
    // This seems like a weird bug... something else includes listenablefuture from guava.
    all*.exclude group: 'com.google.guava', module: 'listenablefuture'
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
      signingConfig signingConfigs.release

      buildConfigField "String", "DEFAULT_SERVER", "\"http://game2.war-worlds.com/\""
    }
    debug {
      buildConfigField "String", "DEFAULT_SERVER", "\"http://127.0.0.1:8080/\""
    }
  }
  packagingOptions {
    // These get added twice from different dependencies, so just ignore them.
    exclude 'LICENSE-EPL-1.0.txt'
    exclude 'LICENSE-EDL-1.0.txt'
  }
}

def getVersionCodeFromGit() {
  // Same as running:
  // git rev-list <checked out branch name> | wc -l
  return Grgit.open(dir: project.buildscript.sourceFile.parentFile.parent).log().size()
}

dependencies {
  coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.1'

  implementation 'androidx.appcompat:appcompat:1.2.0'
  implementation 'androidx.constraintlayout:constraintlayout:2.1.0-alpha2'
  implementation 'androidx.core:core-ktx:1.3.2'
  implementation 'androidx.preference:preference-ktx:1.1.1'
  implementation 'androidx.transition:transition:1.3.1'
  implementation 'com.google.android.material:material:1.3.0-beta01'
  implementation 'com.google.android.gms:play-services-base:17.5.0'
  implementation 'com.google.android.gms:play-services-auth:19.0.0'
  implementation 'com.google.firebase:firebase-core:18.0.0'
  implementation 'com.google.firebase:firebase-messaging:21.0.1'
  implementation 'com.google.guava:guava:24.1-android'
  implementation 'com.squareup.picasso:picasso:2.71828'
  implementation 'com.squareup.wire:wire-runtime:3.1.0'
  implementation 'com.android.support:multidex:1.0.3'
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
  implementation project(':common')
}

apply plugin: 'com.google.gms.google-services'